// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    maven {
        url "http://maven.tterrag.com/"
    }
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "2.1.6"
group= "gigaherz.enderRift"
archivesBaseName = "EnderRift-1.12.0"

minecraft {
    version = "1.12-14.21.1.2430"
    runDir = "run"

    mappings = "snapshot_20170801"

    replace "@VERSION@", project.version
    replaceIn "EnderRiftMod.java"
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency(':GraphLib:'))
        include(dependency(':Commons-1.12.0:'))
    }
    relocate 'gigaherz.common', "${project.group}.shadow.common"
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}

dependencies {
    deobfCompile "gigaherz.commons:Commons-1.12.0:0.6.3"
    deobfCompile "gigaherz.graph:GraphLib:1.3.0"
    deobfCompile "gigaherz.guidebook:Guidebook-1.12.0:1.7.3"

    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.15-8"
    deobfCompile "net.darkhax.tesla:Tesla-1.12:1.0.61"

    deobfCompile "mezz.jei:jei_1.12:4.7.4.80:api"
    runtime "mezz.jei:jei_1.12:4.7.4.80"

}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
